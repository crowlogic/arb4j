
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0' 
}

group = 'org.arblib'
version = '0.99.3'
description = 'arb4j'
java.sourceCompatibility = JavaVersion.VERSION_22

javafx {
    version = "22.0.2" 
    modules = [ 'javafx.swing', 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ]  
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://oss.sonatype.org/content/repositories/releases') }
    maven { url = uri('https://raw.github.com/SpinyOwl/repo/releases') }
    maven { url = uri('https://maven.jzy3d.org/releases/') }
    maven { url = uri('https://amelentev.github.io/mvnrepo/') }
    maven { url = uri('https://repo.maven.apache.org/maven2/') }
    maven { url = uri('https://download.oracle.com/maven') }
}

dependencies {
    api 'org.ow2.asm:asm:9.6'
    api 'org.ow2.asm:asm-commons:9.6'
    api 'org.ow2.asm:asm-util:9.6'
    api 'org.openjfx:javafx-base:22.0.1'
    api 'org.openjfx:javafx-swing:22.0.1'
    api 'org.openjfx:javafx-fxml:22.0.1'
    api 'org.slf4j:slf4j-simple:2.0.11'
    api 'org.scilab.forge:jlatexmath:1.0.7'
    api 'io.fair-acc:chartfx:11.3.1'
    api 'org.controlsfx:controlsfx:11.2.0'
    api 'junit:junit:4.13.2'
    
}

task createDesktopFile {
    doLast {
        def desktopFile = new File("${buildDir}/ExpressionAnalyzer.desktop")
        desktopFile.text = """[Desktop Entry]
Type=Application
Name=Expression Analyzer
Exec=${projectDir}/bin/analyze
Icon=${projectDir}/src/main/resources/ExpressionAnalyzer.png
Terminal=false
Categories=Utility;
"""
    }
    doLast {
        def desktopFile = new File("${buildDir}/TODO.desktop")
        desktopFile.text = """[Desktop Entry]
Type=Application
Name=TODO
Exec=${projectDir}/bin/todo
Icon=${projectDir}/src/main/resources/TODO.png
Terminal=false
Categories=Utility;
"""
    }    
}

task installDesktopFile(type: Copy) {
    dependsOn createDesktopFile
    from "${buildDir}/ExpressionAnalyzer.desktop"
    from "${buildDir}/TODO.desktop"
    into "${System.getProperty('user.home')}/.local/share/applications"
}

build.finalizedBy createDesktopFile
build.finalizedBy installDesktopFile

tasks.withType(Javadoc) {
    options.addStringOption('source', '22') 
    options.addBooleanOption('-enable-preview', true)
    options.addBooleanOption('Xdoclint:none', true) 
    options.addBooleanOption('quiet', true)         
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE;
     from('src/main/resources') {
        include '**/*'
    }
}

java {
    withSourcesJar()
    modularity.inferModulePath.set(true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['--enable-preview']
    options.fork = true
    options.forkOptions.jvmArgs += ['-ea', '--enable-preview']
}


tasks.test {
    jvmArgs '-ea', '--enable-preview', '--enable-native-access=ALL-UNNAMED', '-Djava.library.path=.'
}

task createClasspathManifest {
    doLast {
        def classpathEntries = configurations.runtimeClasspath.collect { it.getAbsolutePath() }.join(':')
        def manifestFile = file('class.path')
        manifestFile.text = "${classpathEntries}\n"
    }
}

tasks.build.dependsOn createClasspathManifest



publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

processResources {
    from('src/main/resources') {
        include '**/*'
    }
    into 'build/classes/java/main'
}